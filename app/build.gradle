apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()
    maven { url "https://jitpack.io" }
}

// This stanza will turn on checking for unchecked conversion and deprecation.
//allprojects {
//    gradle.projectsEvaluated {
//        tasks.withType(JavaCompile) {
//            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
//        }
//    }
//}

ext {
    androidMapsUtilsVersion = '0.3.4'
    assertjVersion = '3.9.0'
    butterknifeVersion = '8.8.1'
    constraintLayoutVersion = '1.1.0'
    daggerVersion = '2.13'
    gsonVersion = '2.8.1'
    guavaVersion = '20.0'
    jsonVersion = '20170516'
    junitVersion = '4.12'
    materialDialogsVersion = '0.9.6.0' // Depends on the android support lib. Their version must match ours.
    multidexVersion = '1.0.3'
    picassoVersion = '2.5.2'
    playServicesVersion = '11.6.2'
    retrofitVersion = '2.3.0'
    robolectricVersion = '3.7'
    rxAndroidVersion = '2.0.1'
    rxJavaVersion = '2.1.7'
    supportLibVersion = '27.0.1'
}

dependencies {
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    implementation "com.afollestad.material-dialogs:core:$materialDialogsVersion" // https://github.com/afollestad/material-dialogs
    implementation "com.afollestad.material-dialogs:commons:$materialDialogsVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:multidex:$multidexVersion"
    implementation "com.android.support:preference-v14:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.google.android.gms:play-services-analytics:$playServicesVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesVersion"
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "com.google.maps.android:android-maps-utils:$androidMapsUtilsVersion"
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    implementation "com.squareup.picasso:picasso:$picassoVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.json:json:$jsonVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "org.robolectric:shadows-multidex:$robolectricVersion"
    testImplementation "org.robolectric:shadows-supportv4:$robolectricVersion"
    testImplementation "junit:junit:$junitVersion"
}

android {
    buildToolsVersion "27.0.3" // also update in .travis.yml

    def doSignRelease = false
    signingConfigs {
        debug {
            storeFile file("../keystores/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        release {
            if (project.hasProperty('keystoreFile')) {
                doSignRelease = true

                // Set these values in local.properties.
                storeFile file(keystoreFile)
                storePassword keystorePassword
                keyAlias keystoreKeyAlias
                keyPassword keystoreKeyPassword
            }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }

        release {
            if (doSignRelease) {
                signingConfig signingConfigs.release
            }
        }
    }

    lintOptions {
        // FIXME(saemy): Make this an error for release builds on travis.
        warning 'MissingTranslation'
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = isPreDexEnabled && !isTravisBuild
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    sourceSets {
        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    def versionMajor = 2
    def versionMinor = 0
    def versionPatch = 3
    def versionBuild = 0 // bump for dogfood builds, public betas, etc.

    defaultConfig {
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild

        minSdkVersion 14
        compileSdkVersion 27 // also update in .travis.yml
        targetSdkVersion 22

        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
